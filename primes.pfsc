OnPrimes = {
    url: "https://resources.proofscape.org/cors/pdf/example/primes.pdf",
    docId: "pdffp:6ad45bf008a7900624260330da212d75",
}

anno Guide @@@
# Key Ideas

In the proof of the infinitude of primes, the key construction is
<doc:>[the number $N$]{
    sel: Pf2.I30,
}.
Having <doc:>[supposed]{
    sel: Pf2.S10
} there were only finitely many primes, we conclude that $N$
must be congruent both <doc:>[to $0$]{
    sel: Pf2.A80
} and <doc:>[to $1$]{
    sel: Pf2.A90
} mod some prime $p$, and this contradiction completes the proof.
@@@

deduc Lem1 {

    asrt C {
        en = "
        Every composite number is divisible
        by some prime number.
        "
    }

    meson = "C"
}


deduc Thm2 {

    asrt C {
        en = "
        There are infinitely
        many prime numbers.
        "
    }

    meson = "C"
}


deduc Pf2 of Thm2.C {

    docInfo = OnPrimes

    supp S10 {
        en = "
        Suppose there were only
        finitely many primes.
        "

        doc = "v2;s1;(1:816:1056:237:365:172:20);n;(1:816:1056:410:364:148:21)"
    }

    intr I20 {
        en = "
        Let all the primes be
        $p_1, p_2, \ldots, p_n$.
        "

        doc = "v2;s1;(1:816:1056:233:389:148:19);n;x+27;y+2;(1:816:1056:385:393:87:16)"
    }

    intr I30 {
        en = "
        Let $N = p_1 p_2 \cdots p_n + 1$.
        "

        doc = "v2;s1;(1:816:1056:581:387:46:22);n;(1:816:1056:188:411:144:21)"
    }

    asrt A40 {
        en = "
        For all $i$, $N > p_i$.
        "

        doc = "v2;s1;(1:816:1056:236:434:48:24)"
    }

    asrt A50 {
        en = "
        For all $i$, $N \neq p_i$.
        "

        doc = "v2;s1;(1:816:1056:387:411:135:22);n;(1:816:1056:525:412:102:23)"
    }

    asrt A60 {
        en = "$N$ is composite"

        doc = "v2;s1;(1:816:1056:317:436:150:20)"
    }

    exis E70 {
        intr I {
            en = "a prime number $p$"

            doc = "v2;s1;z1;(1:816:1056:506:461:94:22)"
        }
        asrt A {
            sy = "$p \mid N$"

            doc = "v2;s1;z2;(1:816:1056:346:459:20:21);(1:816:1056:424:459:81:23);x+1;y+3;(1:816:1056:588:462:12:19)"
        }

        doc = "v2;s1;(1:816:1056:347:459:252:24)"
    }

    asrt A80 {
        sy = "$N \equiv 0 \mod p$"

        doc = "v2;s1;(1:816:1056:340:484:101:22)"
    }

    asrt A90 {
        sy = "$N \equiv 1 \mod p$"

        doc = "v2;s1;(1:816:1056:266:509:100:22)"
    }

    flse F100 contra S10 {
        doc = "v2;s1;(1:816:1056:375:510:130:20)"
    }


    meson = "
    Suppose S10. Let I20. Let I30.
    Then A40, so A50, hence A60.
    Therefore E70, using Lem1.
    From E70.A get A80, but A90 by I30 and E70.I.
    Hence F100, by A80.
    Therefore Thm2.C.
    "
}


anno primorials_plus_one on Pf2.I30 @@@
Under the *reductio* assumption of the proof, that there are only finitely many
primes, the number $N$ always turns out to be prime. But what happens in the real
world, where there are in fact infinitely many primes?

We'll investigate numbers of the form of $N$, and check whether they're actually
prime or not.

Start by picking a prime number $p$.

<param:eg1_p>[]{
    ptype: "Prime",
    tex: "p",
    args: {
        chooserUpperBound: 50,
    }
}

Now we can ask whether "$p$-primorial plus $1$" is a prime number or not,
where by "primorial" we mean a factorial featuring only prime factors.

<disp:>[]{
    import: {
        p: eg1_p,
    },
    build: """
    # Let M be p-primorial
    primes = primerange(p + 1)
    M = 1
    for q in primes:
        M *= q

    # N = M + 1 is the number we want to investigate
    N = M + 1
    N_is_prime = isprime(N)
    N_factors = None if N_is_prime else factorint(N)

    def write_factorization(factors):
        return r' \cdot '.join(
            f'{b}^{{{e}}}' if e > 1 else str(b)
            for b, e in factors.items()
        )

    def spell_out_primorial(Q):
        primes = primerange(Q + 1)
        s = ''
        for k, q in enumerate(reversed(list(primes))):
            if k == 0:
                s += str(q)
            elif k == 3 and q > 3:
                s += r' \cdots 2'
                break
            else:
                s += rf' \cdot {q}'
        return s

    # Now we can build our display HTML
    simplify_M = f' = {M}' if M > 2 else ''

    html = (
        '$p$-primorial is:\n'
        f'$$M = {spell_out_primorial(p)}{simplify_M}$$\n'
        'Let $N$ be $p$-primorial plus $1$:\n'
        f'$$N = M + 1 = {N}$$\n'
    )

    html += (
        '<p>Is $N$ prime?</p>\n'
        f'<p><b>Answer: </b>{"Yes" if N_is_prime else "No"}</p>\n'
    )

    if not N_is_prime:
        html += f'$$N = {write_factorization(N_factors)}$$'

    return html
    """
}

Examining multiple cases at once can sometimes be a way to detect
unexpected patterns. You can examine a range of primes $p$ below.

<disp:>[]{
    build: """
    # BEGIN EDIT
    # Check p-primorial + 1 for all primes p in the interval
    # from a to b.
    a, b = 0, 20
    # END EDIT
    html = '<pre>p, p-primorial + 1, prime or not (factorization below)</pre>\n'
    html += '<pre>--------------------------------</pre>\n'
    P = primerange(b)
    M = 1
    for p in P:
        M *= p
        if p >= a:
            N = M + 1
            ans = isprime(N)
            html += f'<pre>{p}, {N}: {"" if ans else "not "}prime</pre>\n'
            if not ans:
                F = factorint(N)
                html += f'<pre>  {F}</pre>\n'
    return html
    """
}
@@@
